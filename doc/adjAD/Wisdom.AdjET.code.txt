;=======================================================
;This procedure is used to adjust measured ET for the amount
;of canopy present. It uses a lookup table of values if 
;% cover is the measure used for canopy estimation.
;=======================================================
proc AdjustedET( var aWeatherTC TCursor) Number
var
	x,y, p1, p2, p3, p4, p12, 
	p34, localcover, Result number
endvar

	HeaderTC.locate("KeyCode", aWeatherTC."KeyCode")

	if HeaderTC."ETMethod".search("Coeff") > 0 then
		Result = aWeatherTC."RefET" * aWeatherTC."Crop-Coeff"
	else
		;;;;;;;;;;;;;;;;;;;;;;;;;;; new code ;;;;;;;;;;;;;;;;;;;;;;;
		if HeaderTC."ETMethod".search("auto") > 0 then
			localcover = aWeatherTC."PctCover-auto"
		else
			localcover = aWeatherTC."PctCover-man"
		endif
		;;;;;;;;;;;;;;;;;;;;;;;;;;; end new code ;;;;;;;;;;;;;;;;;;;;;;;

		if localcover < 80 then
			y = floor( localcover/10.)
			x = floor( aWeatherTC."RefET" * 100 / 2. - 4)
			if x < -4 then
				x = -4
			endif
			if x > 14 then
				x = 14
			endif
			
			ETTableTC.qlocate(x,y)
			p1 = ETTableTC.AdjET
			ETTableTC.qlocate(x,y+1)
			p2 = ETTableTC.AdjET
			
			if x = 14 then
				p3 = aWeatherTC."RefET"
				p4 = aWeatherTC."RefET"
			else
				ETTableTC.qlocate(x+1,y)
				p3 = ETTableTC.AdjET
				ETTableTC.qlocate(x+1,y+1)
				p4 = ETTableTC.AdjET
			endif
			
			p12 = p1 + (p2 - p1) * ((localcover - 10 * y) / 10.)
			p34 = p3 + (p4 - p3) * ((localcover - 10 * y) / 10.)
			Result = p12 + (p34 - p12) * ((aWeatherTC."RefET" * 100. -
										((x + 4) * 2)) / 2.)
		else
			Result = aWeatherTC."RefET"
		endif

		Result = abs(Result)

		if Result > aWeatherTC."RefET" then
			Result = aWeatherTC."RefET"
		endif
	endif

	RETURN Result
endproc

;=======================================================
;This procedure is used to calculate the AD balance for
; each day.
;=======================================================
proc ADBal(var aWeatherTC TCursor, AdjET number) Number

	RETURN aWeatherTC."DToPcpn" + aWeatherTC."Irrigation" - AdjET
endproc
